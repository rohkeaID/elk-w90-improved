! Copyright (C) 2015 Jon Lafuente and Manh Duc Le, 2017 Arsenii Gerasimov
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

!BOP
! !ROUTINE: writew90mmn
! !INTERFACE:
subroutine genw90input
! !USES:
use modmain
use modw90
use modw90overlap
! !DESCRIPTION:
!   Writes the Mmn and Amn matrices required for Wannier90 to file.
!
! !REVISION HISTORY:
!   Created March 2015 (Jon Lafuente and Manh Duc Le)
!   Upgraded July 2017 (Arsenii Gerasimov)
!EOP
!BOC

implicit none
! local variables
character(20)  :: atomFileName
integer jk,n,m,ig,is,ias,i
integer ikp,inn,ispn
integer nrc,nrci,irco,ia,lm,l,ist,ir
integer reducek0,redkfil,nstsv_,recl
logical exists
real(8) t1
complex(8)     :: z1
logical        :: spinors_lib = .false.
! allocatable arrays
complex(8), allocatable :: expmt(:,:)
complex(8), allocatable :: mmn(:,:,:,:)
complex(8), allocatable :: amn(:,:,:,:)
complex(8), allocatable :: spn_x(:,:),spn_y(:,:),spn_z(:,:)
complex(8), allocatable :: wfmt(:,:,:,:),wfir(:,:,:)
complex(8), allocatable :: wfmtq(:,:,:,:),wfirq(:,:,:)
complex(8), allocatable :: twfmt(:,:,:,:),twfir(:,:,:),twfmt1(:)
real(8),    allocatable :: evalsv_(:)
! automatic arrays
real(8)        :: bqvec(3),bqc(3),vkql(3)
character(256)    filename
character(10)     dat,tim
real(8)           vkl_(3),vec_0(3)

! check that user has defined bands and projections
if(wann_projlines.eq.-1) then
  write(*,*)
  write(*,*) "Error(genw90input): No projections specified - please input &
                                        &using the wann_projections block."
  write(*,*)
  stop
end if
if(wann_nband.eq.-1) then
  write(*,*)
  write(*,*) "Error(genw90input): No bands specified - please input using &
                                                    &the wann_bands block."
  write(*,*)
  stop
end if

! initialise global variables
call init0
reducek0=reducek  ! if reducek=1 was used in ground state calculations,
                  ! need to regenerate the eigenvectors set for the full BZ.
if (reducek0.ne.0) reducek=0
ngridk = wann_ngridk ! AG: should solve
call init1
call init2
call init3

! initialise variables for Wannierization
allocate(wann_atomsymb(natmtot),wann_atompos(3,natmtot))
allocate(nnlist(nkpt,num_nnmax),nncell(3,nkpt,num_nnmax))
allocate(wann_proj_site(3,nstsv))
allocate(wann_proj_l(nstsv),wann_proj_m(nstsv),wann_proj_radial(nstsv))
allocate(wann_proj_zaxis(3,nstsv),wann_proj_xaxis(3,nstsv))
allocate(wann_proj_zona(nstsv))
allocate(wann_proj_exclude_bands_lib(nstsv))
allocate(wann_proj_spin(nstsv))
allocate(wann_proj_quantdir(3,nstsv))
allocate(wann_proj_isrand(nstsv))

! prepare variables for calling lib of wannier90
do is = 1,nspecies
  do ia = 1,natoms(is)
    atomFileName = trim(spfname(is))
    wann_atomsymb(is + ia - 1) = atomFileName(1:(len(trim(atomFileName))-3)) ! erase '.in'
    wann_atompos(:,is + ia - 1) = atposc(:,ia,is) * bohr2angstrom
  enddo ! is, loop over atoms of a species
enddo ! ia, loop over species

if ( nspinor .eq. 2 ) then
  spinors_lib = .true.
end if

! call wannier90 library
call wannier_setup(trim(wann_seedname),ngridk,nkpt,bohr2angstrom*avec,&   !in
                   (1/bohr2angstrom)*bvec,vkl,nstsv,natmtot,&             !in
                   wann_atomsymb,wann_atompos,.false.,spinors_lib,&       !in
                   wann_nntot,nnlist,nncell,wann_nband_total,wann_nwf,&   !out
                   wann_proj_site,wann_proj_l,wann_proj_m,&               !out
                   wann_proj_radial,wann_proj_zaxis,wann_proj_xaxis,&     !out
                   wann_proj_zona,wann_proj_exclude_bands_lib,&           !out
                   wann_proj_spin,wann_proj_quantdir)                     !out

wann_proj_radial = 0
wann_proj_isrand = .false. ! AG: solve later
if (nspinor .eq. 2)Then
  wann_nproj = wann_nwf / 2
else
  wann_nproj = wann_nwf
endif

! open outputting files
! Mmn
filename = trim(wann_seedname)//'.mmn'
open(500,file=filename,action='WRITE',form='FORMATTED')
call date_and_time(date=dat,time=tim)
write(500,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                          dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
write(500,'(3I8)') wann_nband,nkpt,wann_nntot
! Amn
filename = trim(wann_seedname)//'.amn'
open(501,file=filename,action='WRITE',form='FORMATTED')
write(501,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                          dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
if (nspinor.eq.1) then
  write(501,'(3I8)') wann_nband,nkpt,wann_nproj
else
  write(501,'(3I8)') wann_nband,nkpt,wann_nwf
  ! spn
  filename = trim(wann_seedname)//'.spn'
  open(502,file=filename,action='WRITE',form='FORMATTED')
  write(502,'("Generated by ELK on ",A4,"-",A2,"-",A2," at ",A2,":",A2,":",A2)')&
                            dat(1:4),dat(5:6),dat(7:8),tim(1:2),tim(3:4),tim(5:6)
  write(502,'(3I8)') wann_nband,nkpt
end if

! read density and potentials from file
call readstate
! read Fermi energy from a file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr
! generates the trial wavefunctions from the projection definitions read in
call genw90twf

! create the trial wavefunction matrix
allocate(twfmt1(npcmtmax))
allocate(twfmt(npcmtmax,natmtot,nspinor,wann_nproj))
allocate(twfir(ngtot,nspinor,wann_nproj))
twfmt = cmplx(0.d0,0.d0,kind=8)
twfir = cmplx(0.d0,0.d0,kind=8)! trial wavefunction is zero outside muffin-tin.
do ispn=1,nspinor
  do is=1,nspecies
    nrc=nrcmt(is)
    nrci=nrcmti(is)
    irco=nrci+1
    do ia=1,natoms(is)
      ias=idxas(ia,is)
      do n=1,wann_nproj
        if(.not.wann_proj_haswt(n,ias)) cycle
        ! zero the wavefunction
        twfmt1 = cmplx(0.d0,0.d0,kind=8)
        i=1
        do ir=1,nrci
          lm=0
          do l=0,lmaxi
            do m=-l,l
              lm=lm+1
              z1=wann_projclm(lm,ias,n)
              if(abs(z1).gt.1.d-14) then
                twfmt1(i)=twfmt1(i)+z1*wann_projulr(ir,l,ias,n)
              end if
              i=i+1
            end do
          end do
        end do
        do ir=nrci+1,nrc
          lm=0
          do l=0,lmaxo
            do m=-l,l
              lm=lm+1
              z1=wann_projclm(lm,ias,n)
              if(abs(z1).gt.1.d-14) then
                twfmt1(i)=twfmt1(i)+z1*wann_projulr(ir,l,ias,n)
              end if
              i=i+1
            end do
          end do
        end do
        call zbsht(nrc,nrci,twfmt1,twfmt(:,ias,ispn,n))
      end do
    end do
  end do
end do
deallocate(twfmt1)

! check that EVECSV.OUT has all necessary k-points
allocate(evalsv_(nstsv))
redkfil=0
inquire(iolength=recl) vkl_,nstsv_,evalsv_
do ikp=1,nkpt
  exists=.false.
  t1=9.d99
  inquire(file='EVALSV'//trim(filext),exist=exists)
  if(exists) then
    open(70,file='EVALSV'//trim(filext),action='READ',form='UNFORMATTED', &
        access='DIRECT',recl=recl,err=101)
    read(70,rec=ikp,err=101) vkl_,nstsv_,evalsv_
    close(70)
    t1=abs(vkl(1,ikp)-vkl_(1))+abs(vkl(2,ikp)-vkl_(2))+abs(vkl(3,ikp)-vkl_(3))
  end if
101 continue
  if (.not.exists.or.t1.gt.epslat.or.nstsv.ne.nstsv_) then
    redkfil=1
    exit
  end if
end do
! If kpoint not found in saved eigen-values/vectors, then need to recompute EVEC*OUT.
if (redkfil.ne.0) then
  write(*,*) "Info(genw90input): saved k-points do not contain all required &
                                   &k-points. Recalculating wavefunctions..."
! compute the overlap radial integrals
  call olprad
! compute the Hamiltonian radial integrals
  call hmlrad
! generate the spin-orbit coupling radial functions
  call gensocfr
! generate the first- and second-variational eigenvectors and eigenvalues
  call genevfsv
  write(*,*) "Info(genw90input): Wavefunctions recalculated"
end if

! write seedname.eig file for wannier90
filename = trim(wann_seedname)//'.eig'
open(50,file=filename,action='WRITE',form='FORMATTED')
do ikp=1,nkpt
  call getevalsv(filext,ikp,vkl(:,ikp),evalsv_)
  do ig=1,wann_nband
    ist=wann_bands(ig) ! AG :
    write(50,'(2I12,G18.10)') ig,ikp,(evalsv_(ist)-efermi)*27.211385 ! convert to eV
  end do
end do
close(50)
deallocate(evalsv_)

if(nspinor.eq.2) then
  allocate(spn_x(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_y(nkpt,(wann_nband*(wann_nband+1))/2))
  allocate(spn_z(nkpt,(wann_nband*(wann_nband+1))/2))
end if

! loop over k and k+b points
!$OMP PARALLEL DEFAULT(SHARED) &
!$OMP PRIVATE(ikp,inn,jk,bqvec,mmn,amn,vkql,wfmt,wfir,wfmtq,wfirq,expmt)

allocate(wfmt(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfir(ngtot,nspinor,wann_nband))
allocate(expmt(npcmtmax,natmtot))
allocate(wfmtq(npcmtmax,natmtot,nspinor,wann_nband))
allocate(wfirq(ngtot,nspinor,wann_nband))
allocate(mmn(wann_nband,nspinor,wann_nband,nspinor))
allocate(amn(wann_nband,nspinor,wann_nproj,1))

!$OMP DO
do ikp=1,nkpt

  call genwfsvp(.false.,.false.,wann_nband,wann_bands,vkl(:,ikp),wfmt,ngtot,wfir)

  do inn=1,wann_nntot
    jk = nnlist(ikp,inn)
    bqvec=nncell(1:3,ikp,inn)+vkl(:,jk)-vkl(:,ikp)

    ! b-vector in Cartesian coordinates
    call r3mv(bvec,bqvec,bqc)
    ! generate the phase factor function exp(ib.r) in the muffin-tins
    call genexpmt(bqc,expmt)

    ! k+b-vector in lattice coordinates
    vkql = vkl(:,ikp)+bqvec
    call genwfsvp(.false.,.false.,wann_nband,wann_bands,vkql,wfmtq,ngtot,wfirq)

    ! compute Mmn
    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,nspinor,wfmtq,wfirq,mmn,expmt)

!$OMP CRITICAL
    ! write the Mmn matrix elements
    write(500,'(5I8)') ikp,nnlist(ikp,inn),nncell(:,ikp,inn)
    do n=1,wann_nband
      do m=1,wann_nband
        if (nspinor.eq.1) then
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)),&
                                aimag(mmn(m,1,n,1))
        else
          write(500,'(2G18.10)') dble(mmn(m,1,n,1)+mmn(m,2,n,2)),&
                                aimag(mmn(m,1,n,1)+mmn(m,2,n,2))
        end if
      end do
    end do
!$OMP END CRITICAL

  end do ! end loop over b points

  ! calculate the overlap integrals Amn(k)
  amn = cmplx(0.d0,0.d0,kind=8)
  call genw90overlap(wfmt,wfir,wann_nproj,1,twfmt,twfir,amn)
!$OMP CRITICAL
  ! write the Amn matrix elements
  if (nspinor.eq.1) then
    do n=1,wann_nproj
      do m=1,wann_nband
          write(501,'(3I8,2G18.10)') m,    n,ikp,dble(amn(m,1,n,1)),&
                                                aimag(amn(m,1,n,1))
      end do
    end do
  else
    do n=1,wann_nproj
      do m=1,wann_nband
          write(501,'(3I8,2G18.10)') m,2*n-1,ikp,dble(amn(m,1,n,1)),&
                                                aimag(amn(m,1,n,1))
          write(501,'(3I8,2G18.10)') m,  2*n,ikp,dble(amn(m,2,n,1)),&
                                                aimag(amn(m,2,n,1))
      end do
    end do
  end if
!$OMP END CRITICAL

  ! compute spn
  if(nspinor.eq.2) then
    vec_0(1)=0.d0
    vec_0(2)=0.d0
    vec_0(3)=0.d0
    call genexpmt(vec_0,expmt)
    mmn = cmplx(0.d0,0.d0,kind=8)
    call genw90overlap(wfmt,wfir,wann_nband,nspinor,wfmt,wfir,mmn,expmt)
  !$OMP CRITICAL
    is=1
    do m=1,wann_nband
      do n=1,m
        spn_x(ikp,is) = mmn(n,1,m,2)+mmn(n,2,m,1)
        spn_y(ikp,is) = cmplx(0.d0,1.d0,kind=8)*(mmn(n,2,m,1)-mmn(n,1,m,2))
        spn_z(ikp,is) = mmn(n,1,m,1)-mmn(n,2,m,2)
        is=is+1
      end do
    end do

  !$OMP END CRITICAL

  end if

!$OMP CRITICAL
    write(*,'("Info(wannierization): completed ",I6," of ",I6," k-points")')&
                                                                    &ikp,nkpt
!$OMP END CRITICAL

end do !End loop over k points
!$OMP END DO

deallocate(wfmt,wfir)
deallocate(expmt)
deallocate(wfmtq,wfirq)
deallocate(mmn)
deallocate(amn)

!$OMP END PARALLEL

write(*,*)
write(*,*) "Info(wannierization): Mmn and Amn for each k-point are computed"

! close seedname.mmn and seedname.amn
close(500)
close(501)

deallocate(twfmt,twfir)

! write seedname.spn
if(nspinor.eq.2) then
  do ikp=1,nkpt
    do is=1,(wann_nband*(wann_nband+1))/2
      write(502,'(2G18.10)') spn_x(ikp,is)
      write(502,'(2G18.10)') spn_y(ikp,is)
      write(502,'(2G18.10)') spn_z(ikp,is)
    end do
  end do

  ! close seedname.spn
  close(502)

  write(*,*)
  write(*,*) "Info(wannierization): matrix elements of S between Bloch states &
                                                                 &are computed"

  deallocate(spn_x,spn_y,spn_z)
end if

deallocate(wann_atomsymb,wann_atompos)
deallocate(nnlist,nncell)
deallocate(wann_proj_site)
deallocate(wann_proj_l,wann_proj_m,wann_proj_radial)
deallocate(wann_proj_zaxis,wann_proj_xaxis)
deallocate(wann_proj_zona)
deallocate(wann_proj_exclude_bands_lib)
deallocate(wann_proj_spin)
deallocate(wann_proj_quantdir)
deallocate(wann_proj_isrand)

write(*,*)
write(*,*) "Info(wannierization): wannierization completed"

reducek=reducek0

end subroutine genw90input
!EOC
